// Org. source: https://github.com/pest-parser/pest/blob/master/grammars/src/grammars/json.pest

json = _{ SOI ~ (object | array) ~ EOI }

object = { "{" ~ pair ~ ("," ~ pair)* ~ "}" | "{" ~ "}" }
pair   = { string ~ ":" ~ value }

array = { "[" ~ value ~ ("," ~ value)* ~ "]" | "[" ~ "]" }

value = _{ string | number | object | array | bool | null }

string  = { PUSH(quote) ~ inner ~ POP }

inner   = _{ (!PEEK ~ (reference_wrapper | text))* }
reference_wrapper = _{ "${" ~ reference ~ "}" }
reference = { (!"}" ~ ANY)* }
text = { (!("${" | PEEK) ~ ANY)* }

number = @{ "-"? ~ int ~ ("." ~ ASCII_DIGIT+ ~ exp? | exp)? }
int    = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
exp    = @{ ("E" | "e") ~ ("+" | "-")? ~ ASCII_DIGIT+ }

bool = { "true" | "false" }

null = { "null" }

quote = @{ "\"" | "'" | "`"}
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }