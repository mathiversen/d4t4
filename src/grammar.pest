// Org. source: https://github.com/pest-parser/pest/blob/master/grammars/src/grammars/json.pest

data = _{ SOI ~ (object | array) ~ EOI }

object = { "{" ~ pair ~ ("," ~ pair)* ~ ","? ~ "}" | "{" ~ "}" }
pair   = { string ~ ":" ~ value }

array = { "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]" | "[" ~ "]" }

value = _{ null | bool | number | string | object | array  }

string  = { PUSH(quote) ~ inner ~ POP }

inner   = _{ (!PEEK ~ (reference_wrapper | text))* }
reference_wrapper = _{ "${" ~ reference ~ "}" }
reference = @{ (!"}" ~ ANY)* }

text   = @{ (!("${" | PEEK | "\\") ~ ANY)* ~ (escape ~ text)? }
escape  = @{ "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | unicode) }
unicode = @{ "u" ~ ASCII_HEX_DIGIT{4} }

number = @{ "-"? ~ int ~ ("." ~ ASCII_DIGIT+ ~ exp? | exp)? }
int    = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
exp    = @{ ("E" | "e") ~ ("+" | "-")? ~ ASCII_DIGIT+ }

bool = { "true" | "false" }

null = { "null" }

COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

quote = _{ "\"" | "'" }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }